apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion '19.1.0'

    defaultConfig {
        applicationId "se.chalmers.fleetspeak"
        minSdkVersion 17
        targetSdkVersion 19
        versionCode 1
        versionName "0.4"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility org.gradle.api.JavaVersion.VERSION_1_7
        targetCompatibility org.gradle.api.JavaVersion.VERSION_1_7
    }
    sourceSets.main {
        jniLibs.srcDir 'src/main/libs' //set .so files location to libs
        jni.srcDirs = [] //disable automatic ndk-build call
    }

}



dependencies {
    compile 'com.google.android.gms:play-services:7.8.0'
    compile 'com.android.support:cardview-v7:21.0.+'
    compile 'com.android.support:appcompat-v7:19.1.0'
    compile 'com.android.support:recyclerview-v7:+'
    compile files('libs/Automotive-API-1.3-SNAPSHOT.jar')
    compile files('libs/SDP-1.3-SNAPSHOT.jar')
    compile files('libs/VIL-1.3-SNAPSHOT.jar')
    compile 'org.slf4j:slf4j-android:1.7.7'
}

task downloadSDP(type: MyDownload) {
    sourceUrl = 'http://developer.lindholmen.se/repo/artifacts/SDP/SNAPSHOT/SDP-1.3-SNAPSHOT.jar'
    target = file('libs/SDP-1.3-SNAPSHOT.jar')
}
task downloadVilConfig(type: MyDownload) {
    sourceUrl = 'http://developer.lindholmen.se/repo/artifacts/VIL/SNAPSHOT/VIL-1.3-SNAPSHOT.jar'
    target = file('libs/VIL-1.3-SNAPSHOT.jar')
}
task downloadAutomotiveAPI(type: MyDownload) {
    sourceUrl = 'http://developer.lindholmen.se/repo/artifacts/Automotive-API/SNAPSHOT/Automotive-API-1.3-SNAPSHOT.jar'
    target = file('libs/Automotive-API-1.3-SNAPSHOT.jar')
}

class MyDownload extends DefaultTask {
    @Input
    String sourceUrl
    @OutputFile
    File target

    @TaskAction
    void download() {
        ant.get(src: sourceUrl, dest: target)
    }
}

task libsClean(type: Delete) {
    delete 'app/libs'
}
preBuild.dependsOn downloadSDP
preBuild.dependsOn downloadVilConfig
preBuild.dependsOn downloadAutomotiveAPI